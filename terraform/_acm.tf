# Default wildcard cert
resource "aws_acm_certificate" "wildcard" {
  domain_name       = "*.${aws_route53_zone.dev_itc.name}"
  validation_method = "DNS"
}

resource "aws_route53_record" "wildcard_validation" {
  for_each = {
    for dvo in aws_acm_certificate.wildcard.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  ttl             = 60
  type            = each.value.type
  zone_id         = aws_route53_zone.dev_itc.zone_id
}

resource "aws_acm_certificate_validation" "wildcard" {
  certificate_arn         = aws_acm_certificate.wildcard.arn
  validation_record_fqdns = [for record in aws_route53_record.wildcard_validation : record.fqdn]
}

# Cognito auth certificate in us-east-1

resource "aws_acm_certificate" "auth" {
  provider = aws.use1

  domain_name       = "auth.${aws_route53_zone.dev_itc.name}"
  validation_method = "DNS"
}

resource "aws_route53_record" "auth_validation" {
  provider = aws.use1

  for_each = {
    for dvo in aws_acm_certificate.auth.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  ttl             = 60
  type            = each.value.type
  zone_id         = aws_route53_zone.dev_itc.zone_id
}

resource "aws_acm_certificate_validation" "auth" {
  provider = aws.use1

  certificate_arn         = aws_acm_certificate.auth.arn
  validation_record_fqdns = [for record in aws_route53_record.auth_validation : record.fqdn]
}
